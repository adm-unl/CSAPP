# execution starts at address 0
    .pos 0
    irmovq stack, %rsp      # set up stack pointer
    call main               # execute main program
    halt                    # terminate program

# sample linked list
    .align 8
ele1:
    .quad 0x00a
    .quad ele2
ele2:
    .quad 0x0b0
    .quad ele3
ele3:
    .quad 0xc00
    .quad 0


main:   irmovq ele1, %rdi
    call sum_list           # sum_list(list_ptr ls)
    ret

sum_list:    irmovq $8, %r8 # constant 8 
    xorq %rax, %rax         # val = 0
    andq %rdi, %rdi         # set CC
    jmp test
loop:    mrmovq (%rdi), %r9 # get ls->val
    addq %r9, %rax          # val += ls->val 
    rrmovq %rdi, %r10  
    addq %r8, %r10
    mrmovq (%r10), %rdi     # ls = ls->next 
    andq %rdi, %rdi         # set CC
test:    jne loop           # stop when null pointer
    ret

# stack start here
    .pos 0x200
stack:
